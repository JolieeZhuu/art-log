plugins {
	id "java"
	id "org.springframework.boot" version "3.5.3"
	id "io.spring.dependency-management" version "1.1.7"
}

group = "com.project"
version = "0.0.1-SNAPSHOT"

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
    // Tells Gradle to look in the default repositories
    mavenCentral()
    // Crucial line to use gTTS4j
    maven { url 'https://jitpack.io' }
}

dependencies {
	implementation "org.springframework.boot:spring-boot-starter-actuator" // Monitors and manages endpoints and application status
	implementation "org.springframework.boot:spring-boot-starter-data-jpa" // Enables JPA for DB operations
	implementation "org.springframework.boot:spring-boot-starter-validation" // Enables data validation
	implementation "org.springframework.boot:spring-boot-starter-web" // Web and REST API dependency
	
	// DB specific
	implementation "org.flywaydb:flyway-core" // Enables Flyway migration (version controlling and schema management)
	implementation "org.flywaydb:flyway-database-postgresql" // Postgre-specific flyway migration
	runtimeOnly "org.postgresql:postgresql" // Enables PostgreSQL connection (see application.properties)
	
	//implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
	// https://mvnrepository.com/artifact/com.tetradotoxina/gtts4j
	implementation "com.tetradotoxina:gtts4j:2.0.0" // For gTTS4j
    
	annotationProcessor "org.springframework.boot:spring-boot-configuration-processor" // Easier annotating?
	
	// For test running
	testImplementation "org.springframework.boot:spring-boot-starter-test"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"
}

tasks.named("test") {
	useJUnitPlatform()
}

tasks.named("bootJar") {
    archiveFileName.set("artLog.jar") // Change to your desired name
}

// Task to install React dependencies
tasks.register("installReact", Exec) {
    workingDir = file("src/main/frontend")
    commandLine "/usr/local/bin/npm", "install"
}

// Task to build React
tasks.register("buildReact", Exec) {
    dependsOn "installReact"
    workingDir = file("src/main/frontend")
    commandLine "/usr/local/bin/npm", "run", "build"
}

// Task to copy React build to Spring Boot static folder
tasks.register("copyReactBuild", Copy) {
    dependsOn "buildReact"
    from "src/main/frontend/build"
    into "src/main/resources/static"
}

tasks.named("processResources") {
    dependsOn "copyReactBuild"
}